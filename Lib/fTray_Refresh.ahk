fTray_Refresh()
{ ; Courtesy of masato, original version by Noesis: https://www.autohotkey.com/boards/viewtopic.php?p=156072&sid=f3233e93ef8f9df2aaf4d3dd88f320d0#p156072
    /*		Remove any dead icon from the tray menu
    *		Should work both for W7 & W10

    */

    WM_MOUSEMOVE:=0x200
    detectHiddenWin:=A_DetectHiddenWindows
    DetectHiddenWindows On

    allTitles:=["ahk_class Shell_TrayWnd"
            , "ahk_class NotifyIconOverflowWindow"]
    allControls:=["ToolbarWindow321"
            ,"ToolbarWindow322"
            ,"ToolbarWindow323"
            ,"ToolbarWindow324"
            ,"ToolbarWindow325"
            ,"ToolbarWindow326"]
    allIconSizes:=[24,32]

    for id, title in allTitles
    {
        for id, controlName in allControls
        {
            for id, iconSize in allIconSizes
            {
                ControlGetPos xTray,yTray,wdTray,htTray,% controlName,% title
                y:=htTray - 10
                While (y > 0)
                {
                    x:=wdTray - iconSize/2
                    While (x > 0)
                    {
                        point:=(y << 16) + x
                        PostMessage % WM_MOUSEMOVE, 0,% point,% controlName,% title
                        x -= iconSize/2
                    }
                    y -= iconSize/2
                }
            }
        }
    }

    DetectHiddenWindows %detectHiddenWin%
}
